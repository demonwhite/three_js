<!DOCTYPE html>
<html lang="en">
<head>
	<title>Kent's Three.js test</title>
	<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">

		<style>
			body {
				font-family: Monospace;
				background-color: #222;
				margin: 0px;
				overflow: hidden;
			}
			a {
				color: #f80;
			}
				div#audio-player {
				width: 500px;
				height: 60px;
				background-color: #000000;
				padding: 5px;
				margin: 50px auto;
			}
			div#audio-player > div > audio {
				width: 500px;
				background-color: #000;
				float: left;
			}
			div#audio-player > canvas {
				width: 500px;
				height: 30px;
				background-color: #002d3c;
				float: left;
			}

			#loading {
			    font-size: 20px;
			    color: #fff;
			    width: 100px;
			    margin-top: 100px;
			    position: absolute;
			    background-color: #111;
			    padding: 10px;
			    -webkit-border-radius: 10px;
			    -moz-border-radius: 10px;
			    border-radius: 10px;
			    opacity: 0.9;
			    border: 1px solid #ccc;
			    text-align: center;
			}

			#loading a {
			    color: #fff;
			    text-shadow: 0 0 10px #fff;
			    text-decoration: none;
			}

			#loading p{
			    color: #fff;
			    font-size: 18px;
			}

			#togglefft {
			    float: left;
			    margin: 20px 0 0 10px;
			    color: #ff0077;
			}

			#fft { 
			    display: none;
			    margin: 2px 0 0 10px;
			    float: left;    
			}

		</style>
</head>
<body>
	
	<!-- DOM Structure  -->
	<a id="togglefft" href="#">toggle FFT spectrum</a>
	<canvas id="fft" height="50" width="256"></canvas>
	<div id="loading">Loading . . .</div>
	<script type="text/javascript" src="js/lib/perlin.js"></script>
	<script type="text/javascript" src="js/three.min.js"></script>
	<script src="js/lib/TrackballControls.js"></script>
	<script src="js/lib/stats.min.js"></script>
	<!-- Shaders -->
	<!-- *PARTICLE SHADER* -->
	<script type="x-shader/x-vertex" id="pv">

		attribute float size;
		attribute vec3 aColor;

		varying vec3 vColor;

		void main() {

			vColor = aColor;

			vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );

			gl_PointSize = size * ( 300.0 / length( mvPosition.xyz ) );

			gl_Position = projectionMatrix * mvPosition;

		}

	</script>

	<script type="x-shader/x-fragment" id="pf">

		uniform vec3 color;
		uniform sampler2D texture;

		varying vec3 vColor;

		void main() {

			gl_FragColor = vec4( color * vColor, 1.0 );

			gl_FragColor = gl_FragColor * texture2D( texture, gl_PointCoord );

			if ( gl_FragColor.a < ALPHATEST ) discard;

		}

	</script>
	<!-- * VERTEX SHADER * -->
	<script type="x-shader/x-vertex" id="vs">
	varying vec2 vUv;
	varying float noise;

	uniform float time;

	attribute float spike;
	//
	// GLSL textureless classic 3D noise "cnoise",
	// with an RSL-style periodic variant "pnoise".
	// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
	// Version: 2011-10-11
	//
	// Many thanks to Ian McEwan of Ashima Arts for the
	// ideas for permutation and gradient selection.
	//
	// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
	// Distributed under the MIT license. See LICENSE file.
	// https://github.com/ashima/webgl-noise
	//

	vec3 mod289(vec3 x)
	{
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 mod289(vec4 x)
	{
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 permute(vec4 x)
	{
	  return mod289(((x*34.0)+1.0)*x);
	}

	vec4 taylorInvSqrt(vec4 r)
	{
	  return 1.79284291400159 - 0.85373472095314 * r;
	}

	vec3 fade(vec3 t) {
	  return t*t*t*(t*(t*6.0-15.0)+10.0);
	}

	// Classic Perlin noise
	float cnoise(vec3 P)
	{
	  vec3 Pi0 = floor(P); // Integer part for indexing
	  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
	  Pi0 = mod289(Pi0);
	  Pi1 = mod289(Pi1);
	  vec3 Pf0 = fract(P); // Fractional part for interpolation
	  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
	  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
	  vec4 iy = vec4(Pi0.yy, Pi1.yy);
	  vec4 iz0 = Pi0.zzzz;
	  vec4 iz1 = Pi1.zzzz;

	  vec4 ixy = permute(permute(ix) + iy);
	  vec4 ixy0 = permute(ixy + iz0);
	  vec4 ixy1 = permute(ixy + iz1);

	  vec4 gx0 = ixy0 * (1.0 / 7.0);
	  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
	  gx0 = fract(gx0);
	  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
	  vec4 sz0 = step(gz0, vec4(0.0));
	  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
	  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

	  vec4 gx1 = ixy1 * (1.0 / 7.0);
	  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
	  gx1 = fract(gx1);
	  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
	  vec4 sz1 = step(gz1, vec4(0.0));
	  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
	  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

	  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
	  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
	  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
	  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
	  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
	  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
	  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
	  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

	  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
	  g000 *= norm0.x;
	  g010 *= norm0.y;
	  g100 *= norm0.z;
	  g110 *= norm0.w;
	  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
	  g001 *= norm1.x;
	  g011 *= norm1.y;
	  g101 *= norm1.z;
	  g111 *= norm1.w;

	  float n000 = dot(g000, Pf0);
	  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
	  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
	  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
	  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
	  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
	  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
	  float n111 = dot(g111, Pf1);

	  vec3 fade_xyz = fade(Pf0);
	  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
	  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
	  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
	  return 2.2 * n_xyz;
	}

	// Classic Perlin noise, periodic variant
	float pnoise(vec3 P, vec3 rep)
	{
	  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
	  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
	  Pi0 = mod289(Pi0);
	  Pi1 = mod289(Pi1);
	  vec3 Pf0 = fract(P); // Fractional part for interpolation
	  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
	  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
	  vec4 iy = vec4(Pi0.yy, Pi1.yy);
	  vec4 iz0 = Pi0.zzzz;
	  vec4 iz1 = Pi1.zzzz;

	  vec4 ixy = permute(permute(ix) + iy);
	  vec4 ixy0 = permute(ixy + iz0);
	  vec4 ixy1 = permute(ixy + iz1);

	  vec4 gx0 = ixy0 * (1.0 / 7.0);
	  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
	  gx0 = fract(gx0);
	  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
	  vec4 sz0 = step(gz0, vec4(0.0));
	  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
	  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

	  vec4 gx1 = ixy1 * (1.0 / 7.0);
	  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
	  gx1 = fract(gx1);
	  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
	  vec4 sz1 = step(gz1, vec4(0.0));
	  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
	  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

	  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
	  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
	  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
	  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
	  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
	  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
	  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
	  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

	  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
	  g000 *= norm0.x;
	  g010 *= norm0.y;
	  g100 *= norm0.z;
	  g110 *= norm0.w;
	  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
	  g001 *= norm1.x;
	  g011 *= norm1.y;
	  g101 *= norm1.z;
	  g111 *= norm1.w;

	  float n000 = dot(g000, Pf0);
	  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
	  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
	  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
	  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
	  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
	  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
	  float n111 = dot(g111, Pf1);

	  vec3 fade_xyz = fade(Pf0);
	  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
	  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
	  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
	  return 2.2 * n_xyz;
	}
	// End Noise

	float turbulence( vec3 p ) {
	    float w = 100.0;
	    float t = -.5;
	    for (float f = 1.0 ; f <= 10.0 ; f++ ){
	        float power = pow( 2.0, f );
	        t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
	    }
	    return t;
	}

	void main() {

	    vUv = uv;

	    // add time to the noise parameters so it's animated
	    noise = 20.0 *  -.10 * turbulence( .5 * normal + time );
	    float b = 10.0 * pnoise( 0.05 * position + vec3( 2.0 * time ), vec3( 100.0 ) );
	    float displacement = - noise + b;
	    
	    // vec3 newPosition = position + normal * displacement;
	    vec3 newPosition = position + normal * vec3(spike);

	    gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );

	}
	</script>
	<!-- * FRAGMENT SHADER * -->
	<script type="x-shader/x-fragment" id="fs">
	varying vec2 vUv;
	varying float noise;
	uniform sampler2D tExplosion;

	float random( vec3 scale, float seed ){
	    return fract( sin( dot( gl_FragCoord.xyz + seed, scale ) ) * 43758.5453 + seed ) ;
	}

	void main() {

	    // get a random offset
	    float r = .01 * random( vec3( 12.9898, 78.233, 151.7182 ), 0.0 );
	    // lookup vertically in the texture, using noise and offset
	    // to get the right RGB colour
	    vec2 tPos = vec2( 0, 1.0 - 1.3 * noise + r );
	    vec4 color = texture2D( tExplosion, tPos );

	    gl_FragColor = vec4( color.rgb, 1.0 );

	}
	</script>
	<!-- Web Audio Kit basic tests -->
	<!-- <script type="text/javascript" src="js/smControl.js"></script> -->
	<!-- 
	<div id="audio-player">
		<div id="audio"></div>
		<canvas id="bar"></canvas> 
	</div>
	-->

	<!-- Dancer.js -->
	<script src="js/dancer_js/src/dancer.js"></script>
	<script src="js/dancer_js/src/support.js"></script>
	<script src="js/dancer_js/src/kick.js"></script>
	<script src="js/dancer_js/src/adapterWebAudio.js"></script>
	<script src="js/dancer_js/src/adapterMoz.js"></script>
	<script src="js/dancer_js/src/adapterFlash.js"></script>
	<script src="js/dancer_js/lib/fft.js"></script>
	<script src="js/dancer_js/lib/flash_detect.js"></script>
	<script src="js/dancer_js/plugins/dancer.fft.js"></script>
	
	<!-- Kent's stuff -->
	<script type="text/javascript" src="js/loadAudio.js"></script>
	<script type="text/javascript" src="js/kent_three_js.js"></script>
	<script type="text/javascript" src="js/fftDisplay.js"></script>
	


</body>
</html>